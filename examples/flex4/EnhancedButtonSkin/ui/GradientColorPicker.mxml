<?xml version="1.0" encoding="utf-8"?>
<!--
     Copyright (c) 2007 FlexLib Contributors.  See:
     http://code.google.com/p/flexlib/wiki/ProjectContributors

     Permission is hereby granted, free of charge, to any person obtaining a copy of
     this software and associated documentation files (the "Software"), to deal in
     the Software without restriction, including without limitation the rights to
     use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies
     of the Software, and to permit persons to whom the Software is furnished to do
     so, subject to the following conditions:

     The above copyright notice and this permission notice shall be included in all
     copies or substantial portions of the Software.

     THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
     IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
     FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
     AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
     LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
     OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
     SOFTWARE.
-->
<s:HGroup xmlns:fx="http://ns.adobe.com/mxml/2009"
          xmlns:s="library://ns.adobe.com/flex/spark"
          xmlns:mx="library://ns.adobe.com/flex/mx"
          gap="10"
          creationComplete="onCreationComplete()">
  <fx:Script>
    <![CDATA[
      import mx.events.ColorPickerEvent;
      import events.SetStyleEvent;
      import mx.controls.Alert;
      import mx.controls.ColorPicker;

      private var defaultValues:Array = [0xFCFFF4, 0x006699, 0x0066CC, 0x00CCFF, 0xFCFFF4, 0xD1DBBC];

      [Bindable]
      public var configStyleName:String = "";

      [Bindable]
      private var currentValues:Array = [-1, -1, -1, -1, -1, -1];
      private var _numColors:Number = 2;

      private function onCreationComplete():void
      {
        currentValues = defaultValues;
        //currentValues = defaultValues.slice( 0, _numColors - 1 );
        recreateColorPickers();
      }

      [Bindable]
      public function get numColors():Number
      {
        return _numColors;
      }

      public function set numColors(items:Number):void
      {
        _numColors = items;

        var newVals:Array = [];
        for (var i:int = 0; i < items; i++)
        {
          if (currentValues[i] >= 0)
          {
            newVals.push(currentValues[i]);
          }
          else
          {
            newVals.push(defaultValues[i]);
          }

        }
        currentValues = newVals;

        recreateColorPickers();

        onColorChanged();
      }

      public function recreateColorPickers():void
      {
        this.removeAllElements();
        for (var i:int = 0; i < numColors; i++)
        {
          var picker:ColorPicker = new ColorPicker();
          picker.setStyle('color', 0x000000);
          picker.name = "picker" + i;
          picker.selectedColor = currentValues[i];
          picker.addEventListener(ColorPickerEvent.CHANGE, onColorChanged);
          this.addElement(picker);
        }

      }

      private function onColorChanged(evt:Event=null):void
      {
        var newVals:Array = [];

        for (var i:int = 0; i < numColors; i++)
        {
          var picker:ColorPicker = this.getChildByName("picker" + i) as ColorPicker;
          newVals.push(picker.selectedColor);
        }

        currentValues = newVals;

        if (configStyleName != null)
        {
          dispatchEvent(new SetStyleEvent(configStyleName, currentValues));
        }
        //Alert.show( "colors: " + currentValues );
      }
    ]]>
  </fx:Script>
</s:HGroup>
